DECLARE
  ------------------------------------------------------------------
  -- Teszt 4: BOARD_OVERVIEW + AGGREGÁCIÓK
  ------------------------------------------------------------------
  l_board_id  board.id%TYPE;
  l_sprint_id sprint.id%TYPE;
  l_overview  ty_board_overview;

  l_db_col_cnt  NUMBER;
  l_db_task_cnt NUMBER;
  l_error_count NUMBER := 0;

  -- Flag-ek a konkrét mintatáblákhoz
  l_found_db_schema   BOOLEAN := FALSE;
  l_found_hist_task   BOOLEAN := FALSE;
  l_found_closed_user BOOLEAN := FALSE; -- „Alap felhasználók felvétele” (ez már lezárt)

  -- Segéd a task nevekhez
  c_task_db_schema CONSTANT VARCHAR2(200) := 'DB séma kialakítása';
  c_task_hist      CONSTANT VARCHAR2(200) := 'Historizáció implementálása';
  c_task_users     CONSTANT VARCHAR2(200) := 'Alap felhasználók felvétele';
BEGIN
  dbms_output.put_line('-------------------------------------------------------');
  dbms_output.put_line('   TESZT – BOARD_OVERVIEW_PKG.GET_BOARD_OVERVIEW_FNC');
  dbms_output.put_line('-------------------------------------------------------');

  -- Board + sprint ID-k betöltése (PMA / Sprint 1)
  SELECT id INTO l_board_id FROM board WHERE board_name = 'PMA Main Board';

  SELECT id
    INTO l_sprint_id
    FROM sprint
   WHERE board_id = l_board_id
     AND sprint_name = 'Sprint 1';

  -- Függvény meghívása
  l_overview := board_overview_pkg.get_board_overview_fnc(p_board_id  => l_board_id,
                                                          p_sprint_id => l_sprint_id);

  dbms_output.put_line('Board:   ' || l_overview.board_name);
  dbms_output.put_line('Sprint:  ' || l_overview.sprint_name);
  dbms_output.put_line('Oszlopok (overview): ' ||
                       l_overview.column_list.count);

  -- Oszlopszám ellenőrzés
  SELECT COUNT(*)
    INTO l_db_col_cnt
    FROM column_def
   WHERE board_id = l_board_id;

  IF l_db_col_cnt != l_overview.column_list.count
  THEN
    dbms_output.put_line('FAIL 1. Oszlopszám eltérés. DB=' || l_db_col_cnt ||
                         ', overview=' || l_overview.column_list.count);
    l_error_count := l_error_count + 1;
  ELSE
    dbms_output.put_line('OK   1. Oszlopszám egyezik. (' || l_db_col_cnt || ')');
  END IF;

  -- Oszloponként task darabszám ellenőrzés
  FOR i IN 1 .. l_overview.column_list.count
  LOOP
    SELECT COUNT(*)
      INTO l_db_task_cnt
      FROM task t
     WHERE t.board_id = l_board_id
       AND t.column_id = l_overview.column_list(i).column_id
       AND t.sprint_id = l_sprint_id
       AND t.closed_at IS NULL;
  
    IF l_db_task_cnt != l_overview.column_list(i).tasks.count
    THEN
      dbms_output.put_line('FAIL 2. Task darabszám eltérés oszlopban: ' || l_overview.column_list(i)
                           .column_name || '  DB=' || l_db_task_cnt ||
                           ', overview=' || l_overview.column_list(i)
                           .tasks.count);
      l_error_count := l_error_count + 1;
    ELSE
      dbms_output.put_line('OK   2. Task darabszám egyezik oszlopban: ' || l_overview.column_list(i)
                           .column_name || '  (db=' || l_db_task_cnt || ')');
    END IF;
  END LOOP;

  -- Konkrét mintataskok és aggregált mezők crosscheckelése (Minta adatok az insert_date_script.sql ben.)
  FOR i IN 1 .. l_overview.column_list.count
  LOOP
    FOR j IN 1 .. l_overview.column_list(i).tasks.count
    LOOP
      DECLARE
        l_t ty_task_overview := l_overview.column_list(i).tasks(j);
      BEGIN
        IF l_t.title = c_task_db_schema
        THEN
          l_found_db_schema := TRUE;
        
          IF l_t.attachment_count < 1
          THEN
            dbms_output.put_line('FAIL 3. "DB séma kialakítása" attachment_count < 1');
            l_error_count := l_error_count + 1;
          END IF;
        
          IF instr(nvl(l_t.attachment_types, ' '), 'DESIGN') = 0
          THEN
            dbms_output.put_line('FAIL 3. "DB séma kialakítása" attachment_types nem tartalmazza a DESIGN-t');
            l_error_count := l_error_count + 1;
          END IF;
        
          IF instr(lower(nvl(l_t.labels_text, ' ')), 'backend') = 0
          THEN
            dbms_output.put_line('FAIL 3. "DB séma kialakítása" labels_text nem tartalmazza a backend címkét');
            l_error_count := l_error_count + 1;
          END IF;
        
          IF l_t.has_commit <> 'N'
             OR l_t.has_pr <> 'N'
          THEN
            dbms_output.put_line('FAIL 3. "DB séma kialakítása" commit/PR flag hibás (N/N várható)');
            l_error_count := l_error_count + 1;
          END IF;
        END IF;
      
        IF l_t.title = c_task_hist
        THEN
          l_found_hist_task := TRUE;
        
          IF instr(lower(nvl(l_t.labels_text, ' ')), 'backend') = 0
          THEN
            dbms_output.put_line('FAIL 4. "Historizáció implementálása" labels_text nem tartalmazza a backend-et');
            l_error_count := l_error_count + 1;
          END IF;
        
          IF l_t.has_commit <> 'Y'
          THEN
            dbms_output.put_line('FAIL 4. "Historizáció implementálása" has_commit <> Y');
            l_error_count := l_error_count + 1;
          END IF;
        
          IF l_t.has_pr <> 'Y'
          THEN
            dbms_output.put_line('FAIL 4. "Historizáció implementálása" has_pr <> Y');
            l_error_count := l_error_count + 1;
          END IF;
        END IF;
      
        IF l_t.title = c_task_users
        THEN
          l_found_closed_user := TRUE;
        END IF;
      END;
    END LOOP;
  END LOOP;

  -- Resultok kiértékelése és kiírása
  IF NOT l_found_db_schema
  THEN
    dbms_output.put_line('FAIL 3. "DB séma kialakítása" task nem található az overview-ben.');
    l_error_count := l_error_count + 1;
  ELSE
    dbms_output.put_line('OK   3. "DB séma kialakítása" task megtalálva és részben ellenőrizve.');
  END IF;

  IF NOT l_found_hist_task
  THEN
    dbms_output.put_line('FAIL 4. "Historizáció implementálása" task nem található az overview-ben.');
    l_error_count := l_error_count + 1;
  ELSE
    dbms_output.put_line('OK   4. "Historizáció implementálása" task megtalálva és részben ellenőrizve.');
  END IF;

  IF l_found_closed_user
  THEN
    dbms_output.put_line('FAIL 5. "Alap felhasználók felvétele" (lezárt) mégis megjelenik az overview-ben.');
    l_error_count := l_error_count + 1;
  ELSE
    dbms_output.put_line('OK   5. Lezárt task ("Alap felhasználók felvétele") nem szerepel az overview-ben.');
  END IF;

  -- Összefoglaló
  dbms_output.put_line('-------------------------------------------------------');
  dbms_output.put_line('Összes hiba a 4. tesztben: ' || l_error_count);

  IF l_error_count = 0
  THEN
    dbms_output.put_line('RESULT 4.x: BOARD_OVERVIEW működése RENDBEN');
  ELSE
    dbms_output.put_line('RESULT 4.x: BOARD_OVERVIEW hibák találhatók');
  END IF;
END;
/
