CREATE OR REPLACE PACKAGE BODY project_mgmt_pkg IS

  PROCEDURE create_project_prc(p_project_name IN app_project.project_name%TYPE
                              ,p_proj_key     IN app_project.proj_key%TYPE
                              ,p_description  IN app_project.description%TYPE
                              ,p_owner_id     IN app_project.owner_id%TYPE
                              ,p_project_id   OUT app_project.id%TYPE) IS
    l_owner_exists NUMBER;
  BEGIN
    SELECT COUNT(*)
      INTO l_owner_exists
      FROM app_user
     WHERE id = p_owner_id;
  
    IF l_owner_exists = 0
    THEN
      err_log_pkg.log_error(p_module_name    => 'PROJECT',
                            p_procedure_name => 'create_project_prc',
                            p_error_code     => -20330,
                            p_error_msg      => 'Owner user nem létezik.',
                            p_context        => 'owner_id=' || p_owner_id ||
                                                '; project_name=' ||
                                                p_project_name,
                            p_api            => NULL);
      raise_application_error(-20330,
                              'create_project_prc: a megadott owner user nem létezik (id=' ||
                              p_owner_id || ').');
    END IF;
    INSERT INTO app_project
      (project_name
      ,proj_key
      ,description
      ,owner_id)
    VALUES
      (p_project_name
      ,p_proj_key
      ,p_description
      ,p_owner_id)
    RETURNING id INTO p_project_id;
  
  EXCEPTION
    WHEN dup_val_on_index THEN
      err_log_pkg.log_error(p_module_name    => 'PROJECT',
                            p_procedure_name => 'create_project_prc',
                            p_error_code     => -20331,
                            p_error_msg      => 'Projekt kulcs ütközik (PROJ_KEY).',
                            p_context        => 'proj_key=' || p_proj_key,
                            p_api            => NULL);
      raise_application_error(-20331,
                              'create_project_prc: már létezik ilyen projekt kulcs: "' ||
                              p_proj_key || '".');
    
    WHEN OTHERS THEN
      err_log_pkg.log_error(p_module_name    => 'PROJECT',
                            p_procedure_name => 'create_project_prc',
                            p_error_code     => SQLCODE,
                            p_error_msg      => SQLERRM,
                            p_context        => 'project_name=' ||
                                                p_project_name ||
                                                '; proj_key=' || p_proj_key,
                            p_api            => NULL);
      raise_application_error(-20332,
                              'create_project_prc hiba (proj_key="' ||
                              p_proj_key || '"): ' || SQLERRM);
  END create_project_prc;

  PROCEDURE assign_user_to_project_prc(p_project_id   IN project_member.project_id%TYPE
                                      ,p_user_id      IN project_member.user_id%TYPE
                                      ,p_project_role IN project_member.project_role%TYPE) IS
  BEGIN
    INSERT INTO project_member
      (project_id
      ,user_id
      ,project_role)
    VALUES
      (p_project_id
      ,p_user_id
      ,p_project_role);
  
  EXCEPTION
    WHEN dup_val_on_index THEN
      err_log_pkg.log_error(p_module_name    => 'PROJECT',
                            p_procedure_name => 'assign_user_to_project_prc',
                            p_error_code     => -20340,
                            p_error_msg      => 'User már tagja ennek a projektnek.',
                            p_context        => 'project_id=' ||
                                                p_project_id || '; user_id=' ||
                                                p_user_id,
                            p_api            => NULL);
      raise_application_error(-20340,
                              'assign_user_to_project_prc: a user már tagja ennek a projektnek.');
    
    WHEN OTHERS THEN
      err_log_pkg.log_error(p_module_name    => 'PROJECT',
                            p_procedure_name => 'assign_user_to_project_prc',
                            p_error_code     => SQLCODE,
                            p_error_msg      => SQLERRM,
                            p_context        => 'project_id=' ||
                                                p_project_id || '; user_id=' ||
                                                p_user_id,
                            p_api            => NULL);
      raise_application_error(-20341,
                              'assign_user_to_project_prc hiba (project_id=' ||
                              p_project_id || ', user_id=' || p_user_id ||
                              '): ' || SQLERRM);
  END assign_user_to_project_prc;

END project_mgmt_pkg;
/
