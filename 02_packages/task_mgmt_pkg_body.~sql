CREATE OR REPLACE PACKAGE BODY task_mgmt_pkg IS

  --------------------------------------------------------------------
  -- TASK LÉTREHOZÁS
  --------------------------------------------------------------------
  PROCEDURE create_task_prc(p_project_id    IN task.project_id%TYPE
                           ,p_board_id      IN task.board_id%TYPE
                           ,p_column_id     IN task.column_id%TYPE
                           ,p_sprint_id     IN task.sprint_id%TYPE
                           ,p_created_by    IN task.created_by%TYPE
                           ,p_title         IN task.title%TYPE
                           ,p_description   IN task.description%TYPE
                           ,p_status_id     IN task.status_id%TYPE
                           ,p_priority      IN task.priority%TYPE
                           ,p_estimated_min IN task.estimated_min%TYPE
                           ,p_due_date      IN task.due_date%TYPE
                           ,p_task_id       OUT task.id%TYPE) IS
    l_task_key task.task_key%TYPE;
    l_position task.position%TYPE;
  BEGIN
    ------------------------------------------------------------------
    -- 1. Task key generálása projekt alapján (PMA-0001, DEVOPS-0001…)
    ------------------------------------------------------------------
    l_task_key := build_next_task_key_fnc(p_project_id);

    ------------------------------------------------------------------
    -- 2. POSITION meghatározása az oszlopon belül
    --    (ha még nincs task az oszlopban -> 1)
    ------------------------------------------------------------------
    SELECT nvl(MAX(position), 0) + 1
      INTO l_position
      FROM task
     WHERE column_id = p_column_id;

    ------------------------------------------------------------------
    -- 3. Task beszúrása
    ------------------------------------------------------------------
    INSERT INTO task
      (project_id
      ,board_id
      ,column_id
      ,sprint_id
      ,task_key
      ,title
      ,description
      ,status_id
      ,priority
      ,estimated_min
      ,due_date
      ,created_by
      ,position)
    VALUES
      (p_project_id
      ,p_board_id
      ,p_column_id
      ,p_sprint_id
      ,l_task_key
      ,p_title
      ,p_description
      ,p_status_id
      ,p_priority
      ,p_estimated_min
      ,p_due_date
      ,p_created_by
      ,l_position)
    RETURNING id INTO p_task_id;

  EXCEPTION
    WHEN dup_val_on_index THEN
      raise_application_error(-20100,
                              'create_task_prc: ütközés az egyedi constrainten (valószínûleg TASK_KEY). ' ||
                              '(project_id = ' || p_project_id ||
                              ', task_key = "' || l_task_key || '")');
    WHEN OTHERS THEN
      raise_application_error(-20101,
                              'create_task_prc hiba (project_id = ' ||
                              p_project_id || ', title = "' || p_title ||
                              '"): ' || SQLERRM);
  END create_task_prc;

  --------------------------------------------------------------------
  -- TASK–USER HOZZÁRENDELÉS
  --------------------------------------------------------------------
  PROCEDURE assign_user_to_task_prc(p_task_id IN task_assignment.task_id%TYPE
                                   ,p_user_id IN task_assignment.user_id%TYPE) IS
  BEGIN
    INSERT INTO task_assignment
      (task_id
      ,user_id
      ,assigned_at)
    VALUES
      (p_task_id
      ,p_user_id
      ,SYSDATE);

  EXCEPTION
    WHEN dup_val_on_index THEN
      raise_application_error(-20110,
                              'assign_user_to_task_prc: a user már hozzá van rendelve ehhez a taskhoz. ' ||
                              '(task_id = ' || p_task_id || ', user_id = ' ||
                              p_user_id || ')');
    WHEN OTHERS THEN
      raise_application_error(-20111,
                              'assign_user_to_task_prc hiba (task_id = ' ||
                              p_task_id || ', user_id = ' || p_user_id ||
                              '): ' || SQLERRM);
  END assign_user_to_task_prc;

  --------------------------------------------------------------------
  -- TASK MOZGATÁS / SORRENDEZÉS
  --------------------------------------------------------------------

   PROCEDURE move_task_to_column_prc(
    p_task_id       IN task.id%TYPE,
    p_new_column_id IN task.column_id%TYPE,
    p_actor_id      IN task.created_by%TYPE,
    p_new_position  IN task.position%TYPE
  ) IS
    l_old_column_id   task.column_id%TYPE;
    l_old_board_id    task.board_id%TYPE;
    l_old_position    task.position%TYPE;

    l_new_board_id    column_def.board_id%TYPE;
    l_new_status_id   column_def.status_id%TYPE;
    l_wip_limit       column_def.wip_limit%TYPE;

    l_active_count    NUMBER;
    l_final_position  task.position%TYPE;
  BEGIN
    ------------------------------------------------------------------
    -- 1. Task jelenlegi adatai
    ------------------------------------------------------------------
    SELECT column_id, board_id, position
      INTO l_old_column_id, l_old_board_id, l_old_position
      FROM task
     WHERE id = p_task_id;

    ------------------------------------------------------------------
    -- 2. Ha ugyanabba az oszlopba akarjuk "mozgatni", akkor ez csak
    --    sorrendezés, másik prc hívása.
    ------------------------------------------------------------------
    IF p_new_column_id = l_old_column_id THEN
      IF p_new_position IS NOT NULL THEN
        reorder_task_in_column_prc(p_task_id      => p_task_id,
                                   p_new_position => p_new_position);
      END IF;
      RETURN;
    END IF;

    ------------------------------------------------------------------
    -- 3. Új oszlop adatai (board + status + WIP limit)
    ------------------------------------------------------------------
    SELECT board_id, status_id, wip_limit
      INTO l_new_board_id, l_new_status_id, l_wip_limit
      FROM column_def
     WHERE id = p_new_column_id;

    -- Nem engedjük, hogy másik boardra kerüljön át a task
    IF l_new_board_id <> l_old_board_id THEN
      RAISE_APPLICATION_ERROR(
        -20210,
        'move_task_to_column_prc: Task csak ugyanazon a boardon belül mozgatható.'
      );
    END IF;

    ------------------------------------------------------------------
    -- 4. WIP limit ellenõrzés az új oszlopban
    ------------------------------------------------------------------
    IF l_wip_limit IS NOT NULL AND l_wip_limit > 0 THEN
      SELECT COUNT(*)
        INTO l_active_count
        FROM task
       WHERE column_id = p_new_column_id
         AND closed_at IS NULL;

      IF l_active_count >= l_wip_limit THEN
        RAISE_APPLICATION_ERROR(
          -20211,
          'move_task_to_column_prc: WIP limit elérve ebben az oszlopban.'
        );
      END IF;
    END IF;

    ------------------------------------------------------------------
    -- 5. Régi oszlop pozícióinak "összehúzása"
    ------------------------------------------------------------------
    UPDATE task
       SET position = position - 1
     WHERE column_id = l_old_column_id
       AND position > l_old_position;

    ------------------------------------------------------------------
    -- 6. Új oszlopban végsõ pozíció meghatározása
    ------------------------------------------------------------------
    IF p_new_position IS NULL THEN
      -- ha nincs megadva új pozíció, akkor az oszlop végére megy
      SELECT NVL(MAX(position), 0) + 1
        INTO l_final_position
        FROM task
       WHERE column_id = p_new_column_id;
    ELSE
      IF p_new_position < 1 THEN
        RAISE_APPLICATION_ERROR(
          -20212,
          'move_task_to_column_prc: a pozíció nem lehet 1-nél kisebb.'
        );
      END IF;

      -- Hely felszabadítása az új oszlopban a kívánt pozícióra
      UPDATE task
         SET position = position + 1
       WHERE column_id = p_new_column_id
         AND position >= p_new_position;

      l_final_position := p_new_position;
    END IF;

    ------------------------------------------------------------------
    -- 7. Task frissítése: új oszlop, új státusz, új pozíció
    ------------------------------------------------------------------
    UPDATE task
       SET column_id  = p_new_column_id,
           status_id  = l_new_status_id,
           position   = l_final_position,
           updated_at = SYSDATE
     WHERE id = p_task_id;

    -- p_actor_id-t most még nem használjuk, késõbb jó lesz activity loghoz

  EXCEPTION
    WHEN NO_DATA_FOUND THEN
      RAISE_APPLICATION_ERROR(
        -20213,
        'move_task_to_column_prc: a task vagy az új oszlop nem található.'
      );
    WHEN OTHERS THEN
      RAISE_APPLICATION_ERROR(
        -20214,
        'move_task_to_column_prc hiba (task_id=' || p_task_id ||
        ', new_column_id=' || p_new_column_id || '): ' || SQLERRM
      );
  END move_task_to_column_prc;
  ----------------------------------------------------------------------------------------------------------------------------------------

    PROCEDURE reorder_task_in_column_prc(
    p_task_id      IN task.id%TYPE,
    p_new_position IN task.position%TYPE
  ) IS
    l_column_id  task.column_id%TYPE;
    l_old_pos    task.position%TYPE;
  BEGIN
    IF p_new_position < 1 THEN
      RAISE_APPLICATION_ERROR(
        -20220,
        'reorder_task_in_column_prc: a pozíció nem lehet 1-nél kisebb.'
      );
    END IF;

    ------------------------------------------------------------------
    -- 1. Task jelenlegi oszlopa és pozíciója
    ------------------------------------------------------------------
    SELECT column_id, position
      INTO l_column_id, l_old_pos
      FROM task
     WHERE id = p_task_id;

    IF p_new_position = l_old_pos THEN
      RETURN; -- nincs változás
    END IF;

    ------------------------------------------------------------------
    -- 2. Az adott oszlop többi taskjának pozíció-korrekciója
    ------------------------------------------------------------------
    IF p_new_position < l_old_pos THEN
      -- Felfelé mozgatjuk: a köztes taskok lejjebb csúsznak (pos+1)
      UPDATE task
         SET position = position + 1
       WHERE column_id = l_column_id
         AND position >= p_new_position
         AND position <  l_old_pos;
    ELSE
      -- Lefelé mozgatjuk: a köztes taskok feljebb csúsznak (pos-1)
      UPDATE task
         SET position = position - 1
       WHERE column_id = l_column_id
         AND position <= p_new_position
         AND position >  l_old_pos;
    END IF;

    ------------------------------------------------------------------
    -- 3. Kijelölt task új pozíciója
    ------------------------------------------------------------------
    UPDATE task
       SET position   = p_new_position,
           updated_at = SYSDATE
     WHERE id = p_task_id;

  EXCEPTION
    WHEN NO_DATA_FOUND THEN
      RAISE_APPLICATION_ERROR(
        -20221,
        'reorder_task_in_column_prc: a megadott task nem található.'
      );
    WHEN OTHERS THEN
      RAISE_APPLICATION_ERROR(
        -20222,
        'reorder_task_in_column_prc hiba (task_id=' || p_task_id ||
        ', new_position=' || p_new_position || '): ' || SQLERRM
      );
  END reorder_task_in_column_prc;

  ----------------------------------------------------------------------------------------------------------------------------------------
  
END task_mgmt_pkg;
/
